package commands;

import sys.io.Process;
import claw.ClawHelpers;
import sys.io.File;
import haxe.Json;
import Typedefs.LockFile;
import sys.FileSystem;
import claw.Command;

enum abstract IntegrationTypes(String) from String to String {
    var EXECS = 'execs';
    var KEYBINDS = 'keybinds';
    var RULES = 'rules';
}

enum abstract WindowManagers(String) from String to String {
    var HYPRLAND = 'hyprland';
    var NIRI = 'niri';
}

class IntegrationCommand implements Command {
	public var name:String = "integration";
	public var description:String = "integration command for window managers";
	public var arguments:Array<String> = ['wm', "<apply, revoke>"];

    var supportedWindowManagers:Array<WindowManagers> = [HYPRLAND, NIRI]; // :']
    var integrationTypes:Array<IntegrationTypes> = [KEYBINDS, RULES, EXECS];
    var integrationFiles:Map<IntegrationTypes, String> = [];
    var userHyprConfigPath:String = '${Globals.userHomeDir}/.config/hypr/hyprland.conf';

    public function new() {}

	public function execute(args:Array<String>) {
        // this is mainly for dev lol
		if (FileSystem.exists(Globals.whiskerLockFile)) {
			var lock:LockFile = Json.parse(File.getContent(Globals.whiskerLockFile));
			Globals.whiskerQsFolder = lock.folder;
		}
        // init after loading whisker's qs folder in lockfile
        integrationFiles = [
            EXECS => Globals.whiskerQsFolder + "/integrations/hypr-execs.conf",
            KEYBINDS => Globals.whiskerQsFolder + "/integrations/hypr-keybinds.conf",
            RULES => Globals.whiskerQsFolder + "/integrations/hypr-rules.conf"
        ];
        if (args.length == 0)
            throw "no window managers specified " + supportedWindowManagers;
		
        if (!supportedWindowManagers.contains(args[0])) 
            throw 'unsupported window manager "${args[0]}"';

        if (args.length < 2)
            throw 'no action specified [apply, revoke]';
        
        switch (args[1]) {
            case "apply":
                if (args.length <= 2) 
                    throw 'please specify which integrations to apply.\n'
                        + 'supported types: [execs, keybinds, rules]\n'
                        + 'examples:\n'
                        + '  whisker integration ${Utils.getRandom(supportedWindowManagers)} apply execs\n'
                        + '  whisker integration ${Utils.getRandom(supportedWindowManagers)} apply execs,rules\n'
                        + '  whisker integration ${Utils.getRandom(supportedWindowManagers)} apply rules,execs,keybinds';
                if (args.length > 3) 
                    throw 'too many arguments\nnote: you don\'t need spaces when listing integration types.';
                var requestedIntegrations:Array<String> = args[2].trim().split(",");
                for (integration in requestedIntegrations) { 
                    if (!integrationTypes.contains(integration))
                        throw 'integration "$integration" not found';
                }

                switch (args[0]) {
                    case HYPRLAND:
                        Sys.println("checking user's hyprland config file...");
                        if (!FileSystem.exists(userHyprConfigPath))
                            throw 'failed to apply integrations, couldn\'t find user\'s hyprland config ($userHyprConfigPath)';

                        Sys.println('found hyprland.conf!');

                        if (!FileSystem.exists(Globals.whiskerLocalDir)) 
                            FileSystem.createDirectory(Globals.whiskerLocalDir);
                        else if (!FileSystem.isDirectory(Globals.whiskerLocalDir))
                            throw '${Globals.whiskerLocalDir} exists, but it\'s not a directory... why?';

                        Sys.println('generating whisker hyprland config file...');
                        var hyprContent:String = "### Whisker Hyprland Integration File ###\n"
                                                + "## Generated by whisker-cli\n\n";

                        for (integration in requestedIntegrations) {
                            Sys.println('applying "$integration" integration...');
                            var intFile:String = integrationFiles[integration];
                            if (!FileSystem.exists(intFile))
                                throw 'integration file for "$integration" not found at $intFile, is whisker installed properly?';
                            hyprContent += "# Integration: " + integration.toUpperCase() + " #\n";
                            hyprContent += 'source = ${intFile}\n\n';
                        }
                        
                        File.saveContent(Globals.whiskerHyprlandConfig, hyprContent);
                        Sys.println('whisker hyprland config generated at ${Globals.whiskerHyprlandConfig}');
                        Sys.println('appending: source = ${Globals.whiskerHyprlandConfig} to user\'s hyprland config...');

                        var userHyprContent:String = File.getContent(userHyprConfigPath);
                        if (userHyprContent.contains("#whisker_generated_line")) { 
                            Sys.println('whisker integration already applied');
                        } else {
                            userHyprContent += '\n#whisker_generated_line: do not remove these lines';
                            userHyprContent += '\nsource = ${Globals.whiskerHyprlandConfig}';
                            File.saveContent(userHyprConfigPath, userHyprContent);
                            Sys.println('appended successfully!');
                        }

                        Sys.println("reloading hyprland...");
                        var process:Process = new Process('hyprctl', ['reload']);
                        process.exitCode();
                        process.close();
                    case NIRI:
                        Sys.println("niri support later, probably :']");
                }
                Sys.println('successfully integrated whisker with ${args[0]}');
            case "revoke":
                if (args.length > 2) 
                    throw 'too many arguments for revoke';
                
                switch (args[0]) {
                    case HYPRLAND:
                        Sys.println("checking for whisker integration...");
                        if (!FileSystem.exists(userHyprConfigPath))
                            throw 'hyprland config not found at $userHyprConfigPath';
                        
                        var userHyprContent:String = File.getContent(userHyprConfigPath);
                        if (!userHyprContent.contains("#whisker_generated_line")) {
                            Sys.println('no whisker integration found, nothing to revoke');
                            return;
                        }
                        
                        Sys.println('found whisker integration, removing...');
                        var lines:Array<String> = userHyprContent.split("\n");
                        var cleanedLines:Array<String> = [];
                        var skipNext:Bool = false;
                        
                        for (i in 0...lines.length) {
                            if (skipNext) {
                                skipNext = false;
                                continue;
                            }
                            if (lines[i].contains("#whisker_generated_line")) {
                                skipNext = true;
                                continue;
                            }
                            cleanedLines.push(lines[i]);
                        }
                        File.saveContent(userHyprConfigPath, cleanedLines.join("\n"));
                        Sys.println('removed integration from hyprland config');
                        
                        if (FileSystem.exists(Globals.whiskerHyprlandConfig)) {
                            FileSystem.deleteFile(Globals.whiskerHyprlandConfig);
                            Sys.println('deleted whisker hyprland config file');
                        }
                        
                        Sys.println("reloading hyprland...");
                        var process:Process = new Process('hyprctl', ['reload']);
                        process.exitCode();
                        process.close();
                    case NIRI:
                        Sys.println("niri support later, probably :']");
                }
                Sys.println('successfully revoked whisker integration from ${args[0]}');
            default:
                throw 'unknown action "${args[1]}"';
        }

    }
}
